/*
 * Copyright (c) 2015 Jonas Kalderstam.
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.1'
    }
}

apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    google()
    jcenter()
}

// Version number
def versionMajor = 5 // Major UI overhauls
def versionMinor = 7 // Some new functionality
def versionPatch = 1 // Bug fixes
def versionBuild = 3 // Bump for dogfood builds, public betas, etc.

// Version name from git
def getVersionName = { ->
  try {
  def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
  } catch (ignored) {
    return "GITFAILEDINGRADLE"
  }
}

android {
    compileSdkVersion 28
    buildToolsVersion '32.0.0'
    namespace 'com.nononsenseapps.notepad' // for R and BuildConfig
    useLibrary  'org.apache.http.legacy'

    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'

    // dataBinding.enabled = true
    // testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

    lint {
        abortOnError false
        explainIssues true
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE.txt', 'META-INF/NOTICE.txt']
        }
    }

    // disable code shrinking in debug, enable it in release mode
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            pseudoLocalesEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    defaultConfig {
        applicationId "com.nononsenseapps.notepad"
        minSdkVersion 23 // most people have at least android 7.1 anyway: https://apilevels.com/
        targetSdkVersion 28 // see https://github.com/spacecowboy/NotePad/issues/387
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild * 10
        versionName getVersionName()
    }

    compileOptions {
        // coreLibraryDesugaringEnabled true    // enable support for the new language APIs
        // for java 9+ we need target SDK version > 30
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "uselessname"
    productFlavors {
        free {
            dimension "uselessname"
        }
    }

    if (project.hasProperty('STORE_FILE')) {     

      buildTypes {
        release {
        }
      }
    } else {
      println "No key store defined. Signed release not available..."
    }
}

dependencies {
    // implementation 'com.android.support:design:28.0.0'
    // implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'joda-time:joda-time:2.12.0'
    // Dashclock
    implementation 'com.google.android.apps.dashclock:dashclock-api:2.0.0'
    // FilePicker library and its mandatory dependency
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.nononsenseapps:filepicker:3.1.0'
    // OrgParser
    implementation 'org.cowboyprogrammer:orgparser:1.3.1'
    // For Sync
    implementation 'com.squareup.retrofit:retrofit:1.9.0'
    // Included libraries
    implementation project(':external:ActionBar-PullToRefresh')
    implementation project(':external:datetimepicker')
    implementation project(':external:drag-sort-listview')
    implementation project(':external:ShowcaseView')
    // annotations
    annotationProcessor "org.androidannotations:androidannotations:4.8.0"
    implementation "org.androidannotations:androidannotations-api:4.8.0"
    // Tests. this library seems abandoned
    androidTestImplementation 'com.squareup.spoon:spoon-client:1.7.1'
    // libraries to help us migrate to the new format for tests
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    // newer versions would require JAVA 11 and gradle plugin version > 7.4
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.2'
}
